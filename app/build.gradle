plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
}

apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion androidCompileSdkVersion

    defaultConfig {
        applicationId "br.com.hitg.calculator"
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode 25
        versionName "5.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    signingConfigs {
        release {
            storeFile file("$System.env.KEYSTORE" ?: "KeyStore.jks")
            storePassword "$System.env.KEYSTORE_PASSWORD"
            keyAlias "$System.env.KEY_ALIAS"
            keyPassword "$System.env.KEY_PASSWORD"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable = true
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
    }

    lintOptions {
        disable("TypographyFractions", "TypographyQuotes")
        enable("RtlHardcoded", "RtlCompat", "RtlEnabled")
        check("NewApi", "InlinedApi")
        quiet = true
        abortOnError = false
        ignoreWarnings = true
    }
}

dependencies {
    implementation project(path: ':domain')

    def dependencies = rootProject.ext.dependencies
    def testDependencies = rootProject.ext.testDependecies

    implementation dependencies.kotlinJdk7
    implementation dependencies.appCompat
    implementation dependencies.media
    implementation dependencies.legacyv4

    implementation dependencies.recyclerview
    implementation dependencies.constraintlayout
    implementation dependencies.lifecycle

    implementation dependencies.material

    implementation dependencies.firebaseCore
    implementation dependencies.firebaseCrashlytics
    implementation dependencies.firebaseAnalytics

    testImplementation testDependencies.junit
    testImplementation testDependencies.espresso
    testImplementation testDependencies.runner
}

apply plugin: "com.google.gms.google-services"